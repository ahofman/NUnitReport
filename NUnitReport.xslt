<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:msxsl="urn:schemas-microsoft-com:xslt" exclude-result-prefixes="msxsl"
>
    <xsl:output method="html" 
                doctype-system="about:legacy-compat" 
                encoding="utf-8"
                indent="yes"/>

  <xsl:template match="test-results">
    <html>
      <head>
        <style type="text/css">
          body
          {
          font-family:"Trebuchet MS";
          }
          details
          {
          width:100%;
          }
          table,th,td
          {
          border:1px solid grey;
          }
          td
          {
          text-align:center;
          }
          table
          {
          border-collapse:collapse;
          width:100%;
          margin-top:6px;
          margin-bottom:30px;
          }
          h1,h2,p
          {
          margin:3px;
          }
          thead
          {
          background-color:lightgrey;
          }
          th.NameColumn
          {
          width:55%;
          }
          th.StatusColumn
          {
          width:25%;
          text-alignment:center;
          }
          th.TimeColumn
          {
          width:10%;
          }
          th.AssertsColumn
          {
          width:10%;
          }
          td.TestName
          {
          text-align:left;
          }
          td.Success
          {
          background-color:lightgreen;
          }
          td.Failure
          {
          background-color:red;
          }
          td.Inconclusive, td.Ignored
          {
          background-color:lightyellow;
          }
          td.failure-details
          {
          background-color:lightpink;
          font-size:10px;
          text-indent:10px;
          text-align:left;
          }
          td.reason
          {
          background-color:lightgrey;
          font-size:10px;
          text-indent:10px;
          text-align:left;
          }
        </style>
      </head>
      <body>
        <article>
          <h1>Test Results</h1>
          <header>
            <p>Generated by <a href="https://github.com/ahofman/NUnitReport">NUnitReport</a></p>
            <p>Tests run on <time datetime="{@date} {@time}" pubdate="">
              <xsl:value-of select="@date"/>, <xsl:value-of select="@time"/>
              </time>
            </p>
          </header>
          <br/>
          <h2>Summary</h2>
          <table>
            <thead>
              <tr>
                <th>Tests</th>
                <th>Errors</th>
                <th>Failures</th>
                <th>Not Run</th>
                <th>Inconclusive</th>
                <th>Ignored</th>
                <th>Skipped</th>
                <th>Invalid</th>
              </tr>
            </thead>
            <tbody>
              <tr>
                <td>
                  <xsl:value-of select="@total"/>
                </td>
                <td>
                  <xsl:value-of select="@errors"/>
                </td>
                <td>
                  <xsl:value-of select="@failures"/>
                </td>
                <td>
                  <xsl:value-of select="@not-run"/>
                </td>
                <td>
                  <xsl:value-of select="@inconclusive"/>
                </td>
                <td>
                  <xsl:value-of select="@ignored"/>
                </td>
                <td>
                  <xsl:value-of select="@skipped"/>
                </td>
                <td>
                  <xsl:value-of select="@invalid"/>
                </td>
              </tr>
            </tbody>
          </table>
      
          <h2>Test Suites</h2>
          <table>
            <thead>
              <tr>
                <th class="NameColumn">Name</th>
                <th class="StatusColumn">Status</th>
                <th class="TimeColumn">Time (s)</th>
                <th class="AssertsColumn">Asserts</th>
              </tr>
            </thead>
            <tbody>
              <xsl:apply-templates mode="suiteSummary"/>
            </tbody>
          </table>
          <xsl:apply-templates mode="suiteDetails"/>
        </article>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="test-suite[@type='TestFixture']" mode="suiteSummary">
    <tr>
      <td class="TestName">
        <a href="#{@name}FixtureDetails">
          <xsl:value-of select="@name"/>
        </a>
      </td>
      <td class="{@result}">
        <xsl:value-of select="@result"/>
      </td>
      <td>
        <xsl:value-of select="@time"/>
      </td>
      <td>
        <xsl:value-of select="@asserts"/>
      </td>
    </tr>
  </xsl:template>
  
  <xsl:template match="test-suite[@type='TestFixture']" mode="suiteDetails">
    <h2 id="{@name}FixtureDetails">
      <xsl:value-of select="@name"/>
    </h2>
    <table>
      <thead>
        <tr>
          <th class="NameColumn">Name</th>
          <th class="StatusColumn">Status</th>
          <th class="TimeColumn">Time (s)</th>
          <th class="AssertsColumn">Asserts</th>
        </tr>
      </thead>
      <tbody>
        <xsl:apply-templates/>
      </tbody>
    </table>
  </xsl:template>
  
  <xsl:template match="test-case">
    <tr>
      <td  class="TestName">
        <xsl:value-of select="@name"/>
      </td>
      <td class="{@result}">
        <xsl:value-of select="@result"/>
      </td>
      <td>
        <xsl:value-of select="@time"/>
      </td>
      <td>
        <xsl:value-of select="@asserts"/>
      </td>
    </tr>
    <xsl:apply-templates />
  </xsl:template>

  <xsl:template match="failure">
    <tr>
      <td colspan="4" class="failure-details">
        <xsl:value-of select="message" />
        <br />
        <xsl:value-of select="stack-trace" />
      </td>
    </tr>
  </xsl:template>

  <xsl:template match="reason">
    <tr>
      <td colspan="4" class="reason">
        <xsl:value-of select="message" />
      </td>
    </tr>
  </xsl:template>
</xsl:stylesheet>
