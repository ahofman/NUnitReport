<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:msxsl="urn:schemas-microsoft-com:xslt" exclude-result-prefixes="msxsl"
>
    <xsl:output method="html" 
                doctype-system="about:legacy-compat" 
                encoding="utf-8"
                indent="yes"/>

  <xsl:template match="test-results">
    <html>
      <head>
        <style type="text/css">
          body
          {
          font-family:"Trebuchet MS";
          }
          details
          {
          width:100%;
          }
          table
          {
          border:"1";
          width:100%;
          }
          thead
          {
          background-color:lightgrey;
          }
          tr.Success
          {
          background-color:lightgreen;
          }
          tr.Failure
          {
          background-color:red;
          }
          tr.Inconclusive, tr.Ignored
          {
          background-color:lightyellow;
          }
          tr.failure-details
          {
          background-color:lightpink;
          font-size:10px;
          text-indent:10px;
          }
          tr.reason
          {
          background-color:lightgrey;
          font-size:10px;
          text-indent:10px
          }
        </style>
      </head>
      <body>
        <article>
          <h1>Test Results</h1>
          <p>Generated by <a href="https://github.com/ahofman/NUnitReport">NUnitReport</a></p>
          <p>Tests run on <time datetime="{@date} {@time}" pubdate="">
            <xsl:value-of select="@date"/>, <xsl:value-of select="@time"/>
          </time></p>
          <h2>Summary</h2>
          <table>
            <thead>
              <tr>
                <th>Tests</th>
                <th>Errors</th>
                <th>Failures</th>
                <th>Not Run</th>
                <th>Inconclusive</th>
                <th>Ignored</th>
                <th>Skipped</th>
                <th>Invalid</th>
              </tr>
            </thead>
            <tbody>
              <tr>
                <td>
                  <xsl:value-of select="@total"/>
                </td>
                <td>
                  <xsl:value-of select="@errors"/>
                </td>
                <td>
                  <xsl:value-of select="@failures"/>
                </td>
                <td>
                  <xsl:value-of select="@not-run"/>
                </td>
                <td>
                  <xsl:value-of select="@inconclusive"/>
                </td>
                <td>
                  <xsl:value-of select="@ignored"/>
                </td>
                <td>
                  <xsl:value-of select="@skipped"/>
                </td>
                <td>
                  <xsl:value-of select="@invalid"/>
                </td>
              </tr>
            </tbody>
          </table>

          <h2>Test Suites</h2>
          <table>
            <thead>
              <tr>
                <th>Name</th>
                <th>Result</th>
                <th>Time</th>
                <th>Asserts</th>
              </tr>
            </thead>
            <tbody>
              <xsl:apply-templates mode="suiteSummary"/>
            </tbody>
          </table>
          <xsl:apply-templates mode="suiteDetails"/>
        </article>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="test-suite[@type='TestFixture']" mode="suiteSummary">
    <tr class="{@result}">
      <td>
        <a href="#{@name}FixtureDetails">
          <xsl:value-of select="@name"/>
        </a>
      </td>
      <td>
        <xsl:value-of select="@result"/>
      </td>
      <td>
        <xsl:value-of select="@time"/>
      </td>
      <td>
        <xsl:value-of select="@asserts"/>
      </td>
    </tr>
  </xsl:template>
  
  <xsl:template match="test-suite[@type='TestFixture']" mode="suiteDetails">
    <h2 id="{@name}FixtureDetails">
      <xsl:value-of select="@name"/>
    </h2>
    <table>
      <thead>
        <tr>
          <th>Name</th>
          <th>Status</th>
          <th>Time (s)</th>
          <th>Asserts</th>
        </tr>
      </thead>
      <tbody>
        <xsl:apply-templates/>
      </tbody>
    </table>
  </xsl:template>
  
  <xsl:template match="test-case">
    <tr class="{@result}">
      <td>
        <xsl:value-of select="@name"/>
      </td>
      <td>
        <xsl:value-of select="@result"/>
      </td>
      <td>
        <xsl:value-of select="@time"/>
      </td>
      <td>
        <xsl:value-of select="@asserts"/>
      </td>
    </tr>
    <xsl:apply-templates />
  </xsl:template>

  <xsl:template match="failure">
    <tr class="failure-details">
      <td colspan="4">
        <xsl:value-of select="message" />
        <br />
        <xsl:value-of select="stack-trace" />
      </td>
    </tr>
  </xsl:template>

  <xsl:template match="reason">
    <tr class="reason">
      <td colspan="4">
        <xsl:value-of select="message" />
      </td>
    </tr>
  </xsl:template>
</xsl:stylesheet>
